plugins {
    id "org.jetbrains.intellij" version "0.2.18"  // https://plugins.gradle.org/plugin/org.jetbrains.intellij
}

intellij {
    version '181.3007.14'
    updateSinceUntilBuild = true
    publish {
        apply from: "login.properties"
    } 
}

dependencies {
    compile fileTree(dir: 'lib', include: '*.jar')
    testCompile 'junit:junit:4.12'
}

// Simple function to load change-notes.html and description.html into valid text for plugin.xml  (from https://github.com/halirutan/IntelliJ-Key-Promoter-X/blob/master/build.gradle)
def htmlFixer = { f -> file(f).text.replace('<html>', '').replace('</html>', '') }

patchPluginXml {
    version '2.10.1'   // UPDATE ME!!!!
    // see http://www.jetbrains.org/intellij/sdk/docs/basics/getting_started/build_number_ranges.html
    sinceBuild ='162.000'
    untilBuild ='999.000'   
    
    changeNotes = htmlFixer('change-notes.html')
    pluginDescription = htmlFixer('description.html')
}

apply plugin: 'java'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// non-standad layout was used, hence this override
sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
        resources {
            srcDirs = ['testData']
        }
    }
}

repositories {
    maven {
        url 'http://repo1.maven.org/maven2'
    }
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
    }
}
        